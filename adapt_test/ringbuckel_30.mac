!/INPUT,'C:/SHK/Code/adapt_test/ringbuckel_30','mac'
!-===SIMULATION CAPACITOR DISCHARGE WELDING===-!
	finish
	/clear
	/units,si
	!-=INPUT=-!
		/cwd,C:/SHK/Solution/
		filecode = 'adapt_test'
		!PARAMETER
		PROJECTION_H1       = 0.0050
		PROJECTION_H2       = 0.0013
		PROJECTION_L1       = 0.0005
		PROJECTION_R1       = 0.020/2
		PROJECTION_R2       = 0.0140/2
		PROJECTION_R3       = 0.0034
		PROJECTION_PHI_1    = 30
		PROJECTION_PHI_2    = 30
		SHEET_R             = PROJECTION_R1
		SHEET_H             = 0.0020
		T_UNI               = 298
		ARG_ECC             = 50
		ARG_TCC             = 50
		FORCE               = 22000
		pi=ACOS(-1)
	!-=WORKING DIRECTORY=-!
		/inquire,act_path,directory
		/mkdir,%act_path(1,1)%/%filecode%
		/cwd,%act_path(1,1)%/%filecode%
		/filname,filecode,1
		! search only the ANSYS and current working directories
		/psearch,off
		/prep7
	!-=GEOMETRY=-!
		!KEYPOINTS SHEET
			k,100,0,0
			k,101,PROJECTION_R3,0
			*if,PROJECTION_PHI_1,eq,90,then
				k,102,PROJECTION_R2-PROJECTION_L1/2,0
			*else
				k,102,PROJECTION_R2-PROJECTION_L1/2-PROJECTION_H2/(tan(PROJECTION_PHI_1*PI/180)),0
			*endif
			*if,PROJECTION_PHI_2,eq,90,then
				k,103,PROJECTION_R2+PROJECTION_L1/2,0
			*else
				k,103,PROJECTION_R2+PROJECTION_L1/2+PROJECTION_H2/(tan(PROJECTION_PHI_2*PI/180)),0
			*endif
			k,104,PROJECTION_R1,0
			k,105,PROJECTION_R1,-SHEET_H
			*if,PROJECTION_PHI_1,eq,90,then
				k,106,PROJECTION_R2-PROJECTION_L1/2,-SHEET_H
			*else
				k,106,PROJECTION_R2-PROJECTION_L1/2-PROJECTION_H2/(tan(PROJECTION_PHI_1*PI/180)),-SHEET_H
			*endif
			*if,PROJECTION_PHI_2,eq,90,then
				k,107,PROJECTION_R2+PROJECTION_L1/2,-SHEET_H
			*else
				k,107,PROJECTION_R2+PROJECTION_L1/2+PROJECTION_H2/(tan(PROJECTION_PHI_2*PI/180)),-SHEET_H
			*endif
			k,108,PROJECTION_R3,-SHEET_H
			k,109,0,-SHEET_H
		!KEYPOINTS SHEET
			k,200,PROJECTION_R3,PROJECTION_H1
			k,201,PROJECTION_R1,PROJECTION_H1
			k,202,PROJECTION_R1,PROJECTION_H2+PROJECTION_H1*0.30
			k,203,PROJECTION_R1,PROJECTION_H2+PROJECTION_H1*0.10
			k,204,PROJECTION_R1,PROJECTION_H2
			*if,PROJECTION_PHI_2,eq,90,then
				k,205,PROJECTION_R2+PROJECTION_L1/2,PROJECTION_H2
			*else
				k,205,PROJECTION_R2+PROJECTION_L1/2+PROJECTION_H2/(tan(PROJECTION_PHI_2*PI/180)),PROJECTION_H2
			*endif
			k,206,PROJECTION_R2+PROJECTION_L1/2,0
			k,207,PROJECTION_R2-PROJECTION_L1/2,0
			*if,PROJECTION_PHI_1,eq,90,then
				k,208,PROJECTION_R2-PROJECTION_L1/2,PROJECTION_H2
			*else
				k,208,PROJECTION_R2-PROJECTION_L1/2-PROJECTION_H2/(tan(PROJECTION_PHI_1*PI/180)),PROJECTION_H2
			*endif
			k,209,PROJECTION_R3,PROJECTION_H2
			k,210,PROJECTION_R3,PROJECTION_H2+PROJECTION_H1*0.10
			k,211,PROJECTION_R3,PROJECTION_H2+PROJECTION_H1*0.30
		!AREAS
			numstr,area,100
			numstr,line,100
			a,100,101,108,109
			a,101,102,106,108
			a,102,103,107,106
			a,103,104,105,107
			aglue,100,101,102,103
			numstr,area,200
			numstr,line,200
			a,205,206,207,208
			a,203,204,209,210
			a,202,203,210,211
			a,201,202,211,200
			aglue,200,201,202,203
	!-=MATERIALS=-!
		/mplib,read,C:\SHK\Material                        !TODO
		mat,1,
		mpread,'ENAW5083_JMAT_EDIT','si_mpl','',LIB
		MPDATA,MU,1,,1000 
		mat,2,
		mpread,'ENAW6082_JMAT_EDIT','si_mpl','',LIB
		MPDATA,MU,2,,1000 
	!-=ELEMENTS/MATERIAL/MESHING=-!
		!ELEMENT
			et,1,183
			keyopt,1,3,1
			keyopt,1,2,0
			keyopt,1,4,0
			keyopt,1,9,0
			keyopt,1,10,0
		!MATERIAL
			asel,s,,,100,103,1
			aatt,1,,1
			asel,s,,,200,204,1
			aatt,2,,1
			allsel,all,all
			asel,all
			
		mopt,lsmo,on                  !smooth
		esize,0.0002
		allsel,all,all
		asel,s,,,102,200,98
		aesize,all,0.00006
		asel,s,,,101,103,2
		asel,a,,,204
		aesize,all,0.00008
		asel,s,,,100,202,102
		aesize,all,0.0001
		allsel,all,all
		amesh,all
! Creating ADAPTMSH.MAC for Meshing
*create,ADAPTMSH,MAC,'C:\SHK\Solution\adapt_test'
	C***,Subroutine ADAPTMSH.MAC - Lu - adapt_test - 11.12.2019
	! Conta and Terget will always exist, need not to clear
	type,1
	asel,s,,,102,200,98
	aclear,all
	aesize,all,0.00006
	amesh,all
	ksel,s,,,206,207,1
	KESIZE,ALL,0.00004
 	allsel,all
	! Conta and Target element
	mat,2
	r,3
	real,3
	et,2,169
	et,3,172
	r,3,,1.0,0.1,0,
	rmore,,,1.0E20,0.0,1.0,
	rmore,0.0,,1.0,,1.0,0.5
	rmore,,1.0,1.0,0.0,,1.0
	keyopt,3,1,0                                            !selects degrees of freedom
	keyopt,3,2,4                                            !contact algorithm (augmented Lagrangian)
	keyopt,3,3,0                                            !units of normal contact stiffness
	keyopt,3,4,0                                            !location of contact detection point (on gauss point)
	keyopt,3,5,0                                            !automated adjustment
	keyopt,3,6,2                                            !contact stiffness
	keyopt,3,7,0                                            !element level time incrementation control
	keyopt,3,8,0                                            !asymmetric contact selection
	keyopt,3,9,0                                            !effect of initial penetration or gap
	keyopt,3,10,0                                           !contact stiffness update
	keyopt,3,11,0                                           !beam/shell thickness effect
	keyopt,3,12,0                                           !behavior of contact surface (standard,bonded,..)
	nlgeom,on
	! GENERATE THE TARGET SURFACE
	lsel,s,,,107
	type,2
	nsll,s,1
	esln,s,0
	esurf
	! GENERATE THE CONTACT SURFACE
	lsel,s,,,200,202,1
	type,3
	nsll,s,1
	esln,s,0
	esurf
	allsel,all,all
*end
! Creating ADAPTBC.MAC for Boundary Conditions
*create,ADAPTBC,MAC,'C:\SHK\Solution\adapt_test'
	C***,Subroutine ADAPTBC.MAC - Lu - adapt_test - 11.12.2019
	! delet cp
	cpdele,5,,,any
	allsel,all,all
	lsel,s,,,106,112,3
	lsel,a,,,102
	dl,all,,uy,0
	nsel,s,loc,x,0                      
	d,all, ,0, , , ,ux,                 
	lsel,s,,,213                       
	nsll,s,1                            
	cp,5,uy,all                         
	allsel,all,all
	tunif,t_uni	
*end	
! Creating ADAPTSOL.MAC for Solution
*create,ADAPTSOL,MAC,'C:\SHK\Solution\adapt_test'	
	/solu
	antype,0
	kbc,0	! ramp
	autots,on
	nropt,unsym
	neqit,100
	nlgeom,on
	fk,200,fy,-12000
	deltim,0.1,0.1,0.1,on                   
	time,1
	outres,all,all
	allsel,all,all
	solve
	finish
*end
save
/eof
adapt,2,5,,,1,1,1
