!/INPUT,'C:/SHK/Code/Rezoning/ringbuckel_30','mac'
!-===SIMULATION CAPACITOR DISCHARGE WELDING===-!
	finish
	/clear
	/units,si
	!-=INPUT=-!
		/cwd,C:/SHK/Solution/
		filecode = 'Mesh Nonlinear Adaptivity_dt'
		!PARAMETER
		PROJECTION_H1       = 0.0050
		PROJECTION_H2       = 0.0013
		PROJECTION_L1       = 0.0005
		PROJECTION_R1       = 0.020/2
		PROJECTION_R2       = 0.0140/2
		PROJECTION_R3       = 0.0034
		PROJECTION_PHI_1    = 30
		PROJECTION_PHI_2    = 30
		SHEET_R             = PROJECTION_R1
		SHEET_H             = 0.0020
		T_UNI               = 298
		FORCE               = 22000
		DISP 				= -0.0008	! [m]
		pi=ACOS(-1)
		c10=62.3584129
		c01=-37.8485452
		dd=1.E-03
	!-=WORKING DIRECTORY=-!
		/inquire,act_path,directory
		/mkdir,%act_path(1,1)%/%filecode%
		/cwd,%act_path(1,1)%/%filecode%
		/filname,test,1
		/prep7
	!-=GEOMETRY=-!
		!KEYPOINTS LOWER SHEET
			/prep7
			k,100,
			k,101,PROJECTION_R1*1.2,0
		!KEYPOINTS SHEET
			k,200,PROJECTION_R3,PROJECTION_H1
			k,201,PROJECTION_R1,PROJECTION_H1
			k,202,PROJECTION_R1,PROJECTION_H2+PROJECTION_H1*0.30
			k,203,PROJECTION_R1,PROJECTION_H2+PROJECTION_H1*0.10
			k,204,PROJECTION_R1,PROJECTION_H2
			*if,PROJECTION_PHI_2,eq,90,then
				k,205,PROJECTION_R2+PROJECTION_L1/2,PROJECTION_H2
			*else
				k,205,PROJECTION_R2+PROJECTION_L1/2+PROJECTION_H2/(tan(PROJECTION_PHI_2*PI/180)),PROJECTION_H2
			*endif
			k,206,PROJECTION_R2+PROJECTION_L1/2,0
			k,207,PROJECTION_R2-PROJECTION_L1/2,0
			*if,PROJECTION_PHI_1,eq,90,then
				k,208,PROJECTION_R2-PROJECTION_L1/2,PROJECTION_H2
			*else
				k,208,PROJECTION_R2-PROJECTION_L1/2-PROJECTION_H2/(tan(PROJECTION_PHI_1*PI/180)),PROJECTION_H2
			*endif
			k,209,PROJECTION_R3,PROJECTION_H2
			k,210,PROJECTION_R3,PROJECTION_H2+PROJECTION_H1*0.10
			k,211,PROJECTION_R3,PROJECTION_H2+PROJECTION_H1*0.30
		!KEYPOINTS UPER SHEET
			k,300,0,PROJECTION_H1
			k,301,PROJECTION_R1*1.2,PROJECTION_H1
		!AREAS
			numstr,area,200
			numstr,line,200
			a,205,206,207,208
			a,203,204,209,210
			a,202,203,210,211
			a,201,202,211,200
			aglue,200,201,202,203
		!LINES
			numstr,line,100
			/pnum,line,1
			l,100,101
			l,300,301
	!-=MATERIALS=-!
		/mplib,read,C:\SHK\Material                        !TODO
		! mat,1,
		! mpread,'ENAW5083_JMAT_EDIT','si_mpl','',LIB
		! MPDATA,MU,1,,1000 
		mat,1,
		mpread,'ENAW6082_JMAT_EDIT','si_mpl','',LIB
		MPDATA,MU,2,,1000 
		! tb,hyper,1,,2,mooney
		! tbdata,1,c10,c01,dd
		mp,mu,2,0.0
		r,2
	!-=ELEMENTS/MATERIAL/MESHING=-!
		!ELEMENT
			et,1,183
			keyopt,1,3,1
			et,2,169
			et,3,172
			keyopt,3,9,0
			keyopt,3,10,1
		!MATERIAL
			! asel,s,,,100,103,1
			! aatt,1,,1
			asel,s,,,200,204,1
			aatt,1,,1
			allsel,all,all
			
		!MESHING
			mopt,lsmo,on                  !smooth
			esize,0.0003
			allsel,all,all
			asel,s,,,200
			aesize,all,0.00006
			asel,s,,,204
			aesize,all,0.0001
			asel,s,,,202
			aesize,all,0.0002
			asel,all
			amesh,all
	!-=CONTACT ELEMENTS=-!
			! CONTACT PAIRES 1
			mat,2
			real,2
			type,2

			esize,0.0003
			lmesh,101
			allsel,all,all

			type,3
			lsel,s,,,213
			nsll,,1 
			esln,,0 
			esurf 
			allsel

			! CONTACT PAIRES 2
			real,3
			type,2

			esize,0.0003
			lmesh,100,101
			lsel,s,,,100
			esll,s,1
			esurf, ,reverse
			allsel,all

			type,3
			lsel,s,,,200,202,1
			nsll,,1 
			esln,,0 
			esurf   
			alls
		! check the contact definition
		! cncheck
		! elist
	!-=CONDITIONS=-!
		lsel,s,,,101
		nsll,,1

		cp,1,uy,all
		! FK,300,fy,-FORCE 
		d,all,uy,DISP
		d,all,ux,0
		alls

		lsel,s,,,100
		nsll,,1
		d,all,uy,0
		d,all,ux,0
		alls

		! nsel,s,loc,y,0
		! d,all,uy,0
		! alls

!-===SOLVE===-!
	/solu
	esel,,ename,,183
	cm,solid,elem
	allsel
	nlad,solid,add,box,xyzr,0,PROJECTION_R1*1.2,0,PROJECTION_H1
	nlad,solid,on,,,-2
	pred,off
	rescontrol,,all,1
	eresx,no
	nlgeom,on
	time,1
	! kbc,0
	! NSUBST,50,500,50
	deltim,0.01,0.01,0.01
	outres,all,all
	solve
	fini
	


